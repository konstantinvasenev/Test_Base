
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ОсновныеНачисления.Записывать = Истина;
	Движения.ДополнительныеНачисления.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	НачислениеЗарплатыОсновныеНачисления.Сотрудник КАК Сотрудник,
	               |	НачислениеЗарплатыОсновныеНачисления.Подразделение КАК Подразделение,
	               |	НачислениеЗарплатыОсновныеНачисления.ВидРасчета,
	               |	НачислениеЗарплатыОсновныеНачисления.График,
	               |	НачислениеЗарплатыОсновныеНачисления.ДатаНачала,
	               |	НачислениеЗарплатыОсновныеНачисления.ДатаОкончания
	               |ПОМЕСТИТЬ ТЧ_Осн
	               |ИЗ
	               |	Документ.НачислениеЗарплаты.ОсновныеНачисления КАК НачислениеЗарплатыОсновныеНачисления
	               |ГДЕ
	               |	НачислениеЗарплатыОсновныеНачисления.Ссылка = &Ссылка
	               |	И НачислениеЗарплатыОсновныеНачисления.ВидРасчета = &ВидРасчетаТариф
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Сотрудник,
	               |	Подразделение
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	&ПериодРегистрации,
	               |	ТЧ_Осн.Сотрудник,
	               |	ТЧ_Осн.Подразделение,
	               |	ТЧ_Осн.ВидРасчета,
	               |	ТЧ_Осн.График,
	               |	ТЧ_Осн.ДатаНачала КАК ПериодДействияНачало,
	               |	ТЧ_Осн.ДатаОкончания КАК ПериодДействияКонец,
	               |	ЕСТЬNULL(ТарифныеСтавкиСрезПоследних.Ставка, 0) КАК Тариф
	               |ИЗ
	               |	ТЧ_Осн КАК ТЧ_Осн
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТарифныеСтавки.СрезПоследних(&ПериодРегистрации, ) КАК ТарифныеСтавкиСрезПоследних
	               |		ПО ТЧ_Осн.График = ТарифныеСтавкиСрезПоследних.График
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	&ПериодРегистрации,
	               |	НачислениеЗарплатыДополнительрныеНачисления.Сотрудник,
	               |	НачислениеЗарплатыДополнительрныеНачисления.Подразделение,
	               |	НачислениеЗарплатыДополнительрныеНачисления.ВидРасчета,
				   |	НачислениеЗарплатыДополнительрныеНачисления.Процент,
	               |	ДОБАВИТЬКДАТЕ(&ПериодРегистрации, МЕСЯЦ, -1) КАК БазовыйПериодНачало,
	               |	ДОБАВИТЬКДАТЕ(&ПериодРегистрации, СЕКУНДА, -1) КАК БазовыйПериодКонец
	               |ИЗ
	               |	Документ.НачислениеЗарплаты.ДополнительныеНачисления КАК НачислениеЗарплатыДополнительрныеНачисления
	               |ГДЕ
	               |	НачислениеЗарплатыДополнительрныеНачисления.Ссылка = &Ссылка
	               |	И НачислениеЗарплатыДополнительрныеНачисления.ВидРасчета = &ВидРасчетаПремия";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ВидРасчетаТариф", ПланыВидовРасчета.ОсновныеНачисления.Тариф);
	Запрос.УстановитьПараметр("ВидРасчетаПремия", ПланыВидовРасчета.ДополнительныеНачисления.Премия);
	Запрос.УстановитьПараметр("ПериодРегистрации", НачалоМесяца(Дата));
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	Если Не МассивРезультатов[1].Пустой() Тогда 
		Движения.ОсновныеНачисления.Загрузить(МассивРезультатов[1].Выгрузить());
	КонецЕсли;
	
	Если Не МассивРезультатов[2].Пустой() Тогда 
		Движения.ДополнительныеНачисления.Загрузить(МассивРезультатов[2].Выгрузить());
	КонецЕсли;
	
	Движения.Записать();	
	
	Расчет();
	
КонецПроцедуры

Процедура Расчет()
	
	СтруктураПоиска = Новый Структура("НомерСтроки");
	
	Измерения = Новый Массив;
	Измерения.Добавить("Подразделение");
	
	Разрезы = Новый Массив;
	Разрезы.Добавить("Сотрудник");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	Запрос.УстановитьПараметр("Измерения", Измерения);
	Запрос.УстановитьПараметр("Разрезы", Разрезы);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
	               |	ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия КАК ЧасыФакт
	               |ИЗ
	               |	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(Регистратор = &Регистратор) КАК ОсновныеНачисленияДанныеГрафика
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	НомерСтроки";
	Выборка = Запрос.Выполнить().Выбрать();
	
	НаборЗаписей = Движения.ОсновныеНачисления;
	Для Каждого ЗаписьНабора Из НаборЗаписей Цикл 
		СтруктураПоиска.НомерСтроки = ЗаписьНабора.НомерСтроки;
		Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда 
			ЗаписьНабора.Результат = Выборка.ЧасыФакт * ЗаписьНабора.Тариф;
		КонецЕсли;
		Выборка.Сбросить();
	КонецЦикла;
	НаборЗаписей.Записать(,Истина);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДополнительныеНачисленияБазаОсновныеНачисления.НомерСтроки,
	               |	СУММА(ДополнительныеНачисленияБазаОсновныеНачисления.РезультатБаза) КАК База,
	               |	ДополнительныеНачисленияБазаОсновныеНачисления.Процент
	               |ИЗ
	               |	РегистрРасчета.ДополнительныеНачисления.БазаОсновныеНачисления(&Измерения
				   |																	, 
				   |																	&Измерения
				   |																	, 
				   |																	&Разрезы
				   |																	, 
				   |																	Регистратор = &Регистратор
				   |	) КАК ДополнительныеНачисленияБазаОсновныеНачисления
	               |ГДЕ
	               |	ДополнительныеНачисленияБазаОсновныеНачисления.Сотрудник <> ДополнительныеНачисленияБазаОсновныеНачисления.СотрудникРазрез
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ДополнительныеНачисленияБазаОсновныеНачисления.НомерСтроки,
	               |	ДополнительныеНачисленияБазаОсновныеНачисления.Процент";
	Выборка = Запрос.Выполнить().Выбрать();			   
	
	НаборЗаписей = Движения.ДополнительныеНачисления;
	Для Каждого ЗаписьНабора Из НаборЗаписей Цикл 
		СтруктураПоиска.НомерСтроки = ЗаписьНабора.НомерСтроки;
		Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда 
			ЗаписьНабора.Результат = Выборка.База * ЗаписьНабора.Процент / 100;
		КонецЕсли;
		Выборка.Сбросить();
	КонецЦикла;
	НаборЗаписей.Записать();
	
КонецПроцедуры


